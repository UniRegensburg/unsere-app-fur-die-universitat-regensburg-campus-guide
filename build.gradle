// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.5.0-M1'
    ext.kotlin_version = "1.4.30"
    ext.koin_version = "2.2.2"
    ext.nav_version = "2.3.3"
    ext.safe_args_version = "2.3.3"
    ext.google_services_version = "4.3.4"
    ext.detekt_version = "1.15.0"
    ext.tap_target_view_version = "1.13.0"
    ext.mapbox_navigation_version = "1.4.0"
    ext.mapbox_java_sdk_version = "5.8.0"

    repositories {
        google()
        jcenter()
        mavenCentral {url "https://dl.bintray.com/algolia/maven"}
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:$google_services_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$safe_args_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            // see https://docs.mapbox.com/help/troubleshooting/private-access-token-android-and-ios/
            credentials {
                // This should always be `mapbox` (not your username).
                username = 'mapbox'
                password = project.properties['MAPBOX_DOWNLOADS_TOKEN'] ?: ""
            }
        }
    }
}

configurations {
    detekt
}

detekt {
    toolVersion = "$detekt_version"
    input = files("$rootDir/app/")
    config = files("$rootDir/config/detekt/detekt.yml")
    autoCorrect = false
    parallel = true
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

dependencies {
    //Detekt CLI and Formatter
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.15.0"
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:1.15.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAutoFormat(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Autocorrect Kotlin Style Violations."
    config.setFrom(detekt.config)
    setSource(detekt.input)
    if (project.hasProperty("inputFiles")) {
        // split at newline or carriage return to separate commit input files
        def inputArgs = (project.getProperty("inputFiles")).split(/\r?\n/)
        println("####\nInputFiles:\n$inputArgs\n####")
        // use the given cli arguments as input instead of the default input
        setSource(inputArgs)
    }
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
    parallel = true
    autoCorrect = true
}
