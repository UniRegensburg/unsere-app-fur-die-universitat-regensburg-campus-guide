plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "io.gitlab.arturbosch.detekt" version "$detekt_version"
}

apply plugin: 'com.google.gms.google-services'


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "de.ur.explure"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    detekt
}

detekt {
    toolVersion = "$detekt_version"
    input = files("$projectDir/src/")
    config = files("$rootDir/config/detekt/detekt.yml")
    autoCorrect = false
    parallel = true
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //Koin (Dependency Injection)
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    //AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Memory Leak Detection
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:26.2.0')

    //Detekt CLI and Formatter
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.15.0"
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:1.15.0"

    //Mapbox
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.5.0'

}

task detektAutoFormat(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Autocorrect Kotlin Style Violations."
    setSource(detekt.input)
    config.setFrom(detekt.config)
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
    parallel = true
    autoCorrect = true
    //classpath.setFrom(configurations.detekt)
    //jvmTarget = "1.8"
}
